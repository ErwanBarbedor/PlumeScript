/// Test join
@join
  quill
  mark
end
/// Output
quillmark
/// End

/// Test join with sep
@join(sep: \n)
  - quill
  - mark
end
/// Output
quill
mark
/// End

/// Test table
let t = @table
  - quill
  - mark
  quill: ink
end
$t[2], $t.quill
/// Output
mark, ink
/// End

/// Test type
$type($table())\n
$type(nib)\n
$type($(1+1))
/// Output
table
string
number
/// End

/// Test seq 1
for write in seq(2, 5)
  $write
end
/// Output
2345
/// End

/// Test seq 2
for write in seq(3)
  $write
end
/// Output
123
/// End

/// Test seq 3
for write in seq(2, 6, 2)
  $write
end
/// Output
246
/// End

/// Test enumerate
let t = $table(quill, wing, bird)
@join(sep:\n)
  for index, value in enumerate(t)
    - Item $index is $value.
  end
end
/// Output
Item 1 is quill.
Item 2 is wing.
Item 3 is bird.
/// End

/// Test items
let t = $table(first: quill, second: wing, bonus: bird)
@join(sep:\n)
  for key, value in items(t)
    - Item "$key" is _($value)_.
  end
end
/// Output
Item "first" is _(quill)_.
Item "second" is _(wing)_.
Item "bonus" is _(bird)_.
/// End

/// Test items ?named
let t = $table(wing: quill, write, song)
for k, v in items(t)
  $k -> $v\n
end
---\n
for k, v in items(t, ?named)
  $k -> $v\n
end
/// Output
wing -> quill
1 -> write
2 -> song
---
wing -> quill
/// End

/// Test numeric keys 1 (#253)
let t = @table
  - Wing
  Write: Quill
  - Song
end

for k, v in items(t)
  $k -> $v\n
end
/// Output
1 -> Wing
Write -> Quill
2 -> Song
/// End

/// Test numeric keys 2 (#253)
let t = $table()

set t[1] = Wing

for k, v in items(t)
  $k -> $v\n
end
/// Output
1 -> Wing
/// End

/// Test import text 1
$import(tests/plume/toimport/text)
/// Output
wings
/// End

/// Test import text 2
quill\n
$import(./tests/plume/toimport/text)\n
nib
/// Output
quill
wings
nib
/// End

/// Test import lib 1
let lib = $import(./tests/plume/toimport/lib)
$lib.nib
/// Output
quill
/// End

/// Test import lib 2
let static infos = bird
let lib = $import(tests/plume/toimport/lib2)
$lib.wing() $infos
/// Output
blue quill bird
/// End

/// Test lua import
let double = $lua.require(tests/plume/toimport/lib)

$double(3)
/// Output
6
/// End

/// Test len
let t = @table
  - Wing
  - Nib
  quill: blue
end
$len($t)
/// Output
2
/// End

/// Test table append
let t = @table
  - Wing
  - Nib
  quill: blue
end
$len($t)\n
$table.append($t, paint)
$len($t)
/// Output
2
3
/// End

/// Test table remove 1
let t = @table
  - Wing
  - Nib
  quill: blue
end
$len($t)\n
$table.remove($t)\n
$len($t)
/// Output
2
Nib
1
/// End

/// Test table remove 2
let t = @table
  - Wing
  - Nib
  quill: blue
end
$table.remove($t)\n
$t[1]
/// Output
Nib
Wing
/// End

/// Test Error
let t = @table
  - macro()
  end
end

$join($t)
/// Error
Runtime error: invalid value (table) at index 1 in table for 'concat'

Traceback (most recent call first):
  File 'Error', line 6: $join($t)
                         ^^^^^^^^
/// End